plugins {
    id 'java-library'
    id 'maven-publish'
}

allprojects {
    group = 'com.logbull'
    version = '1.0.0'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    if (project.hasProperty("signing.keyId")) {
        apply plugin: 'signing'
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
        withJavadocJar()
        withSourcesJar()
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    test {
        useJUnitPlatform()
    }

    publishing {
        repositories {
            maven {
                name = "OSSRH"
                def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username = System.getenv("OSSRH_USERNAME")
                    password = System.getenv("OSSRH_PASSWORD")
                }
            }
        }
    }

    if (project.hasProperty("signing.keyId")) {
        afterEvaluate {
            signing {
                required { gradle.taskGraph.hasTask("publish") }
                sign publishing.publications
            }
        }
    }
}
