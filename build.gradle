plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.gradleup.nmcp' version '0.0.8' apply false
}

allprojects {
    group = 'com.logbull'
    version = '1.0.0'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'com.gradleup.nmcp'

    java {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
        withJavadocJar()
        withSourcesJar()
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    test {
        useJUnitPlatform()
    }

    afterEvaluate {
        // Configure Maven Central Publishing after publications are created
        nmcp {
            publish("mavenJava") {
                username = project.findProperty("mavenCentralUsername") ?: System.getenv("MAVEN_CENTRAL_USERNAME")
                password = project.findProperty("mavenCentralPassword") ?: System.getenv("MAVEN_CENTRAL_PASSWORD")
                publicationType = "AUTOMATIC"
            }
        }
        
        if (project.hasProperty("signing.keyId")) {
            signing {
                sign publishing.publications
            }
        }
    }
}
